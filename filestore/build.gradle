buildscript {
	ext {
		springBootVersion = '2.1.4.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath 'se.transmode.gradle:gradle-docker:1.2'
		classpath 'org.owasp:dependency-check-gradle:5.2.1'
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'docker'
apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'jacoco'

jacoco {
  toolVersion = "0.8.2"
}

group = 'com.vilma'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

ext {
  set('springCloudVersion', "Greenwich.SR2")
}
dependencies {
	
	implementation('org.springframework.boot:spring-boot-starter-web')
	runtimeOnly('org.springframework.boot:spring-boot-devtools')
	runtimeOnly('com.h2database:h2')
	testImplementation('org.springframework.boot:spring-boot-starter-test')
	compile("org.springframework.boot:spring-boot-starter-data-jpa")
	implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
	compile "io.springfox:springfox-swagger2:2.9.2"
	compile "io.springfox:springfox-swagger-ui:2.9.2"
	compileOnly "org.projectlombok:lombok:1.18.8"
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
  }
}

docker {
    baseImage "openjdk:11-slim"
    maintainer 'Nuthan Kumar "mnuthan@gmail.com"'
}

task buildDocker(type: Docker) {
	applicationName = "fileserver"
	addFile {
		from jar
		rename {'fileserver_app.jar'}
	}
	entryPoint(['java', '-Djava.security.egd=file:/dev/./urandom', '-jar', '/fileserver_app.jar'])
	// for temp variables
	volume("/store/tmp")
	exposePort(9090)
}

buildDocker.dependsOn(build)

// avoid starting eureka client during test phase
tasks.withType(Test) {
    systemProperty 'eureka.client.enabled', 'false'
}