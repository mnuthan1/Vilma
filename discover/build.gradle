buildscript {
	ext {
		springBootVersion = '2.1.4.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath 'se.transmode.gradle:gradle-docker:1.2'
		classpath 'org.owasp:dependency-check-gradle:5.2.1'
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'docker'
apply plugin: 'org.owasp.dependencycheck'

group = 'com.vilma'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

ext {
  set('springCloudVersion', "Greenwich.SR2")
}
dependencies {
  implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-server'
  // required for java 11 or above
  compile group: 'com.sun.xml.bind', name: 'jaxb-core', version: '2.3.0.1'
  compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
  compile group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.3.1'
  // end java 11 dependencies
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
  }
}

docker {
    baseImage "openjdk:11-slim"
    maintainer 'Nuthan Kumar "mnuthan@gmail.com"'
}

task buildDocker(type: Docker) {
	applicationName = "discovery"
	addFile {
		from jar
		rename {'discovery.jar'}
	}
	entryPoint(['java', '-Djava.security.egd=file:/dev/./urandom', '-jar', '/discovery.jar'])
	exposePort(9999)
}

buildDocker.dependsOn(build)